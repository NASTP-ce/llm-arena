#version 450

#define N_THREADS 128

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly buffer weightBuffer { float weight[]; };
layout(binding = 4) readonly uniform configBuffer {
    uint invRmsBufferIndex; // not used
    uint nColumns;
};
layout(binding = 5) readonly buffer invRmsBuffer { float invRms[]; };

shared uint sharedDim;
shared uint sharedXOffset;
shared uint sharedYOffset;
shared float sharedS;

void main() {
    const uint threadIndex = uint(gl_LocalInvocationID.x);

    if (threadIndex == 0) {
        const uint batchIndex = gl_WorkGroupID.y;
        const uint colIndex = gl_WorkGroupID.z;

        const BatchInfo info = infos[batchIndex];
        sharedDim = info.inputSizeX / nColumns;
        const uint offset = sharedDim * colIndex;
        sharedXOffset = info.inputOffset + offset;
        sharedYOffset = info.outputOffset + offset;

        sharedS = invRms[batchIndex * nColumns + colIndex];
    }

    barrier();
    memoryBarrierShared();

    const uint dim = sharedDim;
    const uint xOffset = sharedXOffset;
    const uint yOffset = sharedYOffset;
    const float s = sharedS;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        y[yOffset + i] = (x[xOffset + i] * s) * weight[i];
    }
}
