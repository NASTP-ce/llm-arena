#version 450

#extension GL_EXT_control_flow_attributes : enable

#define N_THREADS 256

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly uniform opConfigBuffer {
    float epsilon;
    uint nColumns;
};

shared BatchInfo sharedInfo;
shared float sums[N_THREADS];

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;
    const uint batchIndex = gl_WorkGroupID.y;
    const uint colIndex = gl_WorkGroupID.z;

    if (threadIndex == 0) {
        sharedInfo = infos[batchIndex];
    }
    memoryBarrierShared();
    barrier();

    const uint dim = sharedInfo.inputSizeX / nColumns;
    const uint offset = sharedInfo.inputOffset + dim * colIndex;

    float sum = 0.0f;
    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        float v = x[offset + i];
        sum += v * v;
    }
    sums[threadIndex] = sum;

    memoryBarrierShared();
    barrier();

    if (threadIndex == 0) {
        sum = 0.0f;
        [[unroll]] for (uint i = 0; i < N_THREADS; i++) {
            sum += sums[i];
        }
        y[batchIndex * nColumns + colIndex] = inversesqrt((sum / float(dim)) + epsilon);
    }
}
